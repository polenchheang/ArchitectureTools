apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "dg2.dmi.com.dagger2"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "dg2.dmi.com.dagger2.Dagger2TestRunner"
    }

    buildTypes {

        debug {
            minifyEnabled false
            testCoverageEnabled = true
            debuggable true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        final ENDPOINT = "ENDPOINT"

        localhost {
            applicationIdSuffix ".dev"
            buildConfigField "String", ENDPOINT, "\"http://192.168.43.130:9000\""
        }

        dev {
            applicationIdSuffix ".dev"
            buildConfigField "String", ENDPOINT, "\"https://productlist-dev.herokuapp.com\""
        }

        stage {
            applicationIdSuffix ".stage"
            buildConfigField "String", ENDPOINT, "\"https://productlist-stage.herokuapp.com\""
        }

        production {
            buildConfigField "String", ENDPOINT, "\"https://productlist.herokuapp.com\""
        }
    }

    sourceSets {
        instrumentTest.setRoot('androidTest')
    }

    dexOptions {
        incremental true
        maxProcessCount 4
        javaMaxHeapSize "2g"
    }
}

final DAGGER_VERSION = '2.0.2'
final ESPRESSO_VERSION = '2.2.1'
final ESPRESSO_RUNNER_VERSION = '0.4.1'
final SUPPORT_VERSION = '23.1.1'
final OKHTTP_VERSION = '3.2.0'
final RETROFIT_VERSION = '2.0.2'

dependencies {
    testCompile 'junit:junit:4.12'

    compile "com.google.dagger:dagger:${DAGGER_VERSION}"
    apt "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"

    compile "com.android.support:design:$SUPPORT_VERSION"
    compile "com.android.support:support-v4:$SUPPORT_VERSION"
    provided 'javax.annotation:jsr250-api:1.0'

    compile 'com.google.code.gson:gson:2.6.2'
    compile "com.squareup.retrofit2:converter-gson:${RETROFIT_VERSION}"

    compile 'com.jakewharton:butterknife:7.0.1'

    compile "com.squareup.retrofit2:retrofit:${RETROFIT_VERSION}"
    compile "com.squareup.okhttp3:logging-interceptor:${OKHTTP_VERSION}"
    compile 'io.reactivex:rxandroid:1.2.0'
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    compile 'io.reactivex:rxjava:1.1.5'
    // Need to add this when create retrofit .addCallAdapterFactory(RxJavaCallAdapterFactory.create())
    compile "com.squareup.retrofit2:adapter-rxjava:${RETROFIT_VERSION}"

    androidTestCompile "com.squareup.okhttp3:mockwebserver:${OKHTTP_VERSION}"

    androidTestCompile "com.android.support.test:runner:${ESPRESSO_RUNNER_VERSION}"
    androidTestCompile "com.android.support.test:rules:${ESPRESSO_RUNNER_VERSION}"
    androidTestCompile "com.android.support.test.espresso:espresso-core:${ESPRESSO_VERSION}"
    androidTestCompile "com.android.support.test.espresso:espresso-contrib:${ESPRESSO_VERSION}"
    androidTestApt "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"
}

configurations.all {
    resolutionStrategy.force "com.android.support:support-annotations:$SUPPORT_VERSION"
}

configurations.compile.dependencies.each { compileDependency ->
    println "Excluding compile dependency: ${compileDependency.getName()}"
    configurations.androidTestCompile.dependencies.each { androidTestCompileDependency ->
        configurations.androidTestCompile.exclude module: "${compileDependency.getName()}"
    }
}
